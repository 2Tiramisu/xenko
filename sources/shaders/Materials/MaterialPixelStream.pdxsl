// Copyright (c) 2014 Silicon Studio Corp. (http://siliconstudio.co.jp)
// This file is distributed under GPL v3. See LICENSE.md for details.
namespace SiliconStudio.Paradox.Effects.Materials
{
    class MaterialPixelStream : MaterialStream, NormalStream, LightStream
    {
        // --------------------------------------------------
        // Values defined by materials
        // --------------------------------------------------

        // Surface attributes
        stage stream float3 matNormal;

        // Diffuse attributes
        stage stream float4 matDiffuse;

        // Microsurface attributes
        stage stream float  matGlossiness;

        // Specular attributes
        stage stream float3 matSpecular;

        stage stream float  matSpecularIntensity;
        // Occlusion attributes
        stage stream float  matAmbientOcclusion;
        stage stream float  matAmbientOcclusionDirectLightingFactor;
        stage stream float  matCavity;
        stage stream float  matCavityDiffuse;
        stage stream float  matCavitySpecular;

        // Emissive attributes
        stage stream float3  matEmissive;
        stage stream float   matEmissiveIntensity;

        // Inputs while shading a material surface
        stage stream float3 shadingView;
    
        // --------------------------------------------------
        // Values Precomputed before lighting
        // --------------------------------------------------
        
        stage stream float3 matDiffuseVisible;
        
        stage stream float alphaRoughness; // disney-burley roughness

        stage stream float3 matSpecularVisible;
        
        stage stream float NdotV; // normal dot view

        override void ResetMaterialPixelStream()
        {
            base.ResetMaterialPixelStream();

            // Reset all values for material stream to avoid pulling from a different stage (VS...etc.)
            // TODO: It might be interesting to support pulling from VS, but this should be done from the IMaterialSurface and dedicated ComputerColors
            streams.matNormal = float3(0, 0, 1);

            streams.matDiffuse = 0.0f; 
            streams.matDiffuseVisible = 0.0f;      

            streams.matSpecular = 0.0f;        
            streams.matSpecularVisible = 0.0f;      
            streams.matSpecularIntensity = 1.0f;

            streams.matGlossiness = 0.0f;
            streams.alphaRoughness = 1.0f;

            streams.matAmbientOcclusion = 1.0f;  // 0.0: occluded, 1.0: not occluded
            streams.matAmbientOcclusionDirectLightingFactor = 0.0f;

            streams.matCavity = 1.0f;
            streams.matCavityDiffuse = 0.0f;
            streams.matCavitySpecular = 0.0f;
        }

        void PrepareMaterialForLightingAndShading()
        {
            // Direct lighting can be slightly influenced by AO map
            streams.lightDirectAmbientOcclusion = lerp(1.0, streams.matAmbientOcclusion, streams.matAmbientOcclusionDirectLightingFactor);

            // Diffuse visible 
            streams.matDiffuseVisible = streams.matDiffuse.rgb * lerp(1.0f, streams.matCavity, streams.matCavityDiffuse);
            streams.matSpecularVisible = streams.matSpecular.rgb * streams.matSpecularIntensity * lerp(1.0f, streams.matCavity, streams.matCavitySpecular);

            streams.NdotV = max(dot(streams.normalVS, streams.shadingView), 0.0001f);

            // Calculate shading roughtness using Disney-Burley method
            // roughness-burley = roughness^2 = (1 - glossy)^2
            var roughness = 1.0f - streams.matGlossiness;
            // Make sure alphaRoughness is not going below a certain value as it can generate Infinity with some specular model
            streams.alphaRoughness = max(roughness * roughness, 0.0001); 
            // TODO: precalculate alphaRoughness^2
        }
    };
}

