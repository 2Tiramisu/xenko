// Copyright (c) 2014 Silicon Studio Corp. (http://siliconstudio.co.jp)
// This file is distributed under GPL v3. See LICENSE.md for details.
using SiliconStudio.Paradox.Effects.Data;
using SiliconStudio.Paradox.Effects;

namespace SiliconStudio.Paradox.Effects.Core
{
    /// <summary>
    /// Base shader
    /// </summary>
    partial shader ParadoxBaseShader
    {
        using params LightingKeys;
        using params MaterialParameters;

        mixin ShaderBase;
        mixin ShadingBase;
        mixin TransformationWAndVP;
        mixin PositionVSStream;

        // -----------------------------------------------
        // Performs normal mapping (in case of no-skinning, otherwise it is overloaded below)
        // -----------------------------------------------
        if (MaterialParameters.HasNormalMap)
        {
            mixin NormalVSFromNormalMapping;
        }
        else
        {
            mixin NormalVSFromMesh;
        }

        // -----------------------------------------------
        // Performs animation skinning (position, normal and tangent)
        // -----------------------------------------------
        if (MaterialParameters.HasSkinningPosition)
        {
            if (MaterialParameters.SkinningBones > MaterialParameters.SkinningMaxBones)
            {
                // TODO: We should use next power of two minus constant to limit shader permutations?
                MaterialParameters.SkinningMaxBones = MaterialParameters.SkinningBones;
            }
            mixin macro MaterialParameters.SkinningMaxBones;
            mixin TransformationSkinning;

            if (MaterialParameters.HasSkinningNormal)
            {
                mixin NormalSkinning;
            }

            if (MaterialParameters.HasSkinningTangent)
            {
                mixin TangentSkinning;
            }

            if (MaterialParameters.HasSkinningNormal)
            {
                if (MaterialParameters.HasNormalMap)
                {
                    mixin NormalVSSkinningNormalMapping;
                }
                else
                {
                    mixin NormalVSSkinningFromMesh;
                }
            }
        }

        // -----------------------------------------------
        // Mix material and lighting shading
        // -----------------------------------------------
        mixin MaterialSurfaceCompositor;

        // -----------------------------------------------
        // Add light groups
        // -----------------------------------------------
        var lightGroups = LightingKeys.LightGroups;
        for(int i = 0; i < lightGroups.Length; i++)
        {
            mixin compose lightGroups += lightGroups[i];
        }

        // -----------------------------------------------
        // Mix ShadowMap caster 
        // -----------------------------------------------
        if (LightingKeys.CastShadows)
            mixin child ShadowMapCaster;

    };
}