// Copyright (c) 2014 Silicon Studio Corp. (http://siliconstudio.co.jp)
// This file is distributed under GPL v3. See LICENSE.md for details.

namespace SiliconStudio.Paradox.Effects.Images
{
    /// <summary>
    /// A tonemap shader
    /// </summary>
    internal class ToneMapShader : ImageEffectShader
    {
        // Exposure
        stage float KeyValue;
        stage bool  AutoKeyValue;
        stage float LuminanceLocalFactor;
        stage float LuminanceAverageGlobal;
        stage float LinearWhite;

        // Color/Gamma correction
        stage float Gamma;
        stage float Contrast;
        stage float Brightness;

        // ToneMap Operator
        compose ToneMapOperatorShader ToneMapOperator;

        stage override float4 Shading()
        {
            // Get the input color to tonemap
	        float3 color = Texture0.Sample(PointSampler, streams.TexCoord).rgb;

	        // Gets the local luminance
	        float luminanceAverageLocal = Texture1.Sample(LinearSampler, streams.TexCoord).r;

            // Calculate average geometric mean for luminance using local and global average luminances
	        float avgLuminance = lerp(LuminanceAverageGlobal, luminanceAverageLocal, LuminanceLocalFactor);
	        avgLuminance = exp2(avgLuminance);
	        avgLuminance = max(avgLuminance, 0.0001f);

	        // Apply brightness and contrast
	        float globalAverageLum = exp2(LuminanceAverageGlobal);
	        //color = max(color + globalAverageLum.xxx * Brightness, 0.0001);
	        color = max(color + globalAverageLum.xxx * Brightness, 0.0001);
	        color = max(lerp(globalAverageLum.xxx, color, Contrast + 1.0f), 0.0001);

            // Apply ToneMapping
	        color = ToneMap(color, avgLuminance, 0);

	        return float4(pow(color, 1.0/Gamma), 1.0);
        }

        float CalculateExposure(float avgLuminance, float threshold)
        {    
	        float keyValue = AutoKeyValue ? 1.03f - (2.0f / (2 + log10(avgLuminance + 1))) : KeyValue;
	        float linearExposure = (keyValue / avgLuminance);
	        float exposure = log2(max(linearExposure, 0.0001f));
	        exposure = exposure - threshold;
	        return exp2(exposure);
        }

        float3 ToneMap(float3 color, float avgLuminance, float threshold)
        {
	        float exposure = CalculateExposure(avgLuminance, threshold);
	        color *= exposure;
	        color = ToneMapOperator.Compute(color);
	        return color;
        }
    };
}
