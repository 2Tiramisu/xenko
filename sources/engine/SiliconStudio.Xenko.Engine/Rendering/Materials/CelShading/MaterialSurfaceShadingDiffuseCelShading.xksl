// Copyright (c) 2014 Silicon Studio Corp. (http://siliconstudio.co.jp)
// This file is distributed under GPL v3. See LICENSE.md for details.
namespace SiliconStudio.Xenko.Rendering.Materials
{
    /// <summary>
    /// Performs a Cel shading
    /// </summary>
    class MaterialSurfaceShadingDiffuseCelShading<bool TIsEnergyConservative> : IMaterialSurfaceShading, Math, MaterialPixelShadingStream, LightStream, ShadowGroup
    {
        compose IMaterialCelShadingCelLightFunction celLightFunction;

        // TODO USE LAMBERT DIRECTLY: Maybe we can expose this when we support different models
        // MaterialSurfaceShadingDiffuseLambert lambertModel;

        override float3 ComputeDirectLightContribution()
        {
            float oldLightColorNdotL = streams.lightColorNdotL;
            streams.lightColorNdotL = celLightFunction.Compute(streams.lightColorNdotL);
            //float celNdotL = celLightFunction.Compute(streams.NdotL);
            //streams.lightColorNdotL = streams.lightColor * streams.shadowColor * celNdotL * streams.lightDirectAmbientOcclusion;

            var diffuseColor = streams.matDiffuseVisible;
            if (TIsEnergyConservative)
            {
                // Approximation see: http://research.tri-ace.com/Data/course_note_practical_implementation_at_triace.pdf
                diffuseColor *= (1 - streams.matSpecularVisible);
            }

            float3 result =  diffuseColor / PI * streams.lightColorNdotL * streams.matDiffuseSpecularAlphaBlend.x;

            streams.lightColorNdotL = oldLightColorNdotL;
            return result;
        }

        override float3 ComputeEnvironmentLightContribution()
        {
            // TODO: Check how to factorize this with DirectLight
            var diffuseColor = streams.matDiffuseVisible;
            if (TIsEnergyConservative)
            {
                diffuseColor *= (1 - streams.matSpecularVisible);
            }

            float celLight = celLightFunction.Compute(streams.envLightDiffuseColor);
            return diffuseColor * celLight;
        }
    };
}